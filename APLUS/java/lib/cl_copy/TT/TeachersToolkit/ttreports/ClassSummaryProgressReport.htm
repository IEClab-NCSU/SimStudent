<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method="html" encoding="UTF-8" />
	<xsl:param name="useIds"/>

	<xsl:template match="/Report">
		<div style="font-weight: bold; font-size: medium; color: #5582d5">CLASS SUMMARY PROGRESS REPORT</div>
		<xsl:element name="table" use-attribute-sets="top-table">
			<!-- top-table -->
			<tr style="height: 10px;">
				<!-- EMPTY ROW to add spacing -->
				<td colspan="4"></td>
			</tr>
			<tr>
				<td style="font-weight: bold" width="70">STUDENT</td>
				<td style="font-weight: bold" width="70">All</td>
				<td style="font-weight: bold; padding: 0 0 0 30px;" width="120">INSTRUCTOR</td>
				<td><xsl:value-of select="@instructor_name" /></td>
			</tr>
			<tr valign="top">
				<td style="font-weight: bold">DATE</td>
				<td><xsl:value-of select="@timestamp" /></td>
				<td style="font-weight: bold; padding: 0 0 0 30px;" valign="top">CLASS</td>
				<td><xsl:value-of select="@class_name" /></td>
			</tr>
			<tr style="height: 10px;">
				<!-- EMPTY ROW to add spacing -->
				<td colspan="4"></td>
			</tr>
			<!-- END top-table -->
		</xsl:element>
		<xsl:element name="table" use-attribute-sets="main-table">
			<xsl:variable name="unit_count" select="Curriculum/@unit_count" />
			<tr style="background: #5582d5; color: #ffffff;">
				<th style="text-align: left; margin-right: 1px; margin-color: #ffffff;">STUDENT</th>
				<th style="text-align: left; margin-right: 9px;" width="12%">UNIT - SECTION</th>
				<th style="text-align: left;">UNIT PROGRESS (of <xsl:value-of select="$unit_count" />)</th>
			</tr>
			<xsl:for-each select="Curriculum/Student">
				<xsl:variable name="bkgd">
					<xsl:choose>
						<xsl:when test="position() mod 2 = 1">#ffffff</xsl:when>
						<xsl:otherwise>#e4eef7</xsl:otherwise>
					</xsl:choose>
				</xsl:variable>
				<tr style="background: {$bkgd};">
					<td>
						<xsl:choose>
							<xsl:when test="$useIds = 'true'">
								<xsl:choose>
									<xsl:when test="@id != ''">
										<xsl:value-of select="@id" />
									</xsl:when>
									<xsl:otherwise>
										-
									</xsl:otherwise>
								</xsl:choose>
							</xsl:when>
							<xsl:otherwise>
								<xsl:value-of select="@name" />
							</xsl:otherwise>
						</xsl:choose>
					</td>
					<td><b><xsl:value-of select="@current_unit" /></b> - <xsl:value-of select="@current_section" /></td>
					<td>
						<table><tr>
								<xsl:call-template name="unit-blocks">
									<xsl:with-param name="total" select="$unit_count" />
									<xsl:with-param name="completed" select="@units_completed" />
								</xsl:call-template>
						</tr></table>
					</td>
				</tr>
			</xsl:for-each>
			<tr style="background: #5582d5;"><td colspan="3"></td></tr>
			<!-- END main table -->
		</xsl:element>
	</xsl:template>

	<!-- recursive function which generates a solid-colored table cell
	for each completed unit, an outlined cell for others -->
	<xsl:template name="unit-blocks">
		<xsl:param name="total" />
		<xsl:param name="completed" />
		<xsl:if test="$total > 0">
			<xsl:choose>
				<xsl:when test="$completed > 0">
					<td style="margin-left: 1px; width: 6px; height: 12px; background: #5582d5; border-width: 1px; border-style: solid;"></td>
				</xsl:when>
				<xsl:otherwise>
					<td style="margin-left: 1px; width: 6px; height: 12px; border-color: #5582d5; border-width: 1px; border-style: solid;"></td>
				</xsl:otherwise>
			</xsl:choose>
			<xsl:call-template name="unit-blocks">
				<xsl:with-param name="total" select="$total - 1" />
				<xsl:with-param name="completed" select="$completed - 1" />
			</xsl:call-template>
		</xsl:if>
	</xsl:template>

	<xsl:attribute-set name="top-table">
		<xsl:attribute name="border">0</xsl:attribute>
		<xsl:attribute name="align">left</xsl:attribute>
		<xsl:attribute name="cellpadding">0</xsl:attribute>
		<xsl:attribute name="cellspacing">0</xsl:attribute>
		<!-- needs BOTH css attribute clear: both and html
		attribute clear="both" so it will be respected in
		all browsers and the HTML panel -->
		<xsl:attribute name="clear">both</xsl:attribute>
		<xsl:attribute name="style">font-size: small; clear: both;</xsl:attribute>
	</xsl:attribute-set>

	<xsl:attribute-set name="main-table">
		<xsl:attribute name="border">0</xsl:attribute>
		<xsl:attribute name="align">left</xsl:attribute>
		<xsl:attribute name="cellpadding">1</xsl:attribute>
		<xsl:attribute name="cellspacing">0</xsl:attribute>
		<xsl:attribute name="width">620</xsl:attribute>
		<!-- needs BOTH css attribute clear: both and html
		     attribute clear="both" so it will be respected in
		     all browsers and the HTML panel -->
		<xsl:attribute name="clear">both</xsl:attribute>
		<xsl:attribute name="style">font-size: small; clear: both;</xsl:attribute>
	</xsl:attribute-set>

</xsl:stylesheet>
