(require* templates "test/edu/cmu/pact/jess/AdditionChaining.clp")
(require* templates "edu/cmu/pact/jess/AdditionChaining.clp")
(require* templates "AdditionChaining.clp")

;; FOCUS-ON-FIRST-COLUMN
;; IF
;;    The goal is to do an addition problem
;;    And there is no pending subgoal (i.e., we've just started the problem)
;;    And C is the rightmost column of the table
;; THEN
;;    Set a subgoal to process column C
(defrule focus-on-first-column 
;	(addition 
;		(problem ?problem))
	?problem <- (problem 
		(subgoals )
		(interface-elements $? ?table $?))
	?table <- (table 
		(columns $? ?right-column))
	?right-column <- (column 
		(cells $? ?first-addend ?second-addend ?result))
	?first-addend <- (cell 
		(value ?num1))
	?second-addend <- (cell 
		(value ?num2))
	?result <- (cell
        (name ?sum-name) 
		(value nil))
=>
    (predict-observable-action ?sum-name NotSpecified NotSpecified)
    (construct-message [Start with the column 
			on the right. This is the ones column ])
    (bind ?current-sub-goal (assert (process-column-goal
		(column ?right-column)
		(first-addend ?num1)
		(second-addend ?num2))))
	(bind ?work-to-do (assert (finish-problem-goal
		(begin-rule focus-on-first-column))))
	(modify ?problem
		(subgoals (create$ ?current-sub-goal ?work-to-do)))
)

;; FOCUS-ON-NEXT-COLUMN
;; IF
;;    The goal is to do an addition problem
;;    And there is no pending subgoal
;;    And C is the rightmost column with numbers to add and no result
;; THEN
;;    Set a subgoal to process column C

(defrule focus-on-next-column 
;	(addition
;		(problem ?problem))
	?problem <- (problem
		(subgoals ?work-to-do)
		(interface-elements $? ?table $?))
	?work-to-do <- (finish-problem-goal)
	?table <- (table 
		(columns $? ?next-column ?previous-column $?))
	?previous-column <- (column
		(cells $? ?previous-result))
	?previous-result <- (cell 
		(value ?val&:(neq ?val nil)))
	?next-column <- (column 
		(name ?col-name)
		(cells ?carry ?first-addend ?second-addend ?result)
		(position ?pos))
	?result <- (cell
		(value nil))
	?carry <- (cell 
		(value ?num0))
	?first-addend <- (cell 
		(value ?num1))
	?second-addend <- (cell 
		(value ?num2))
=>
	(construct-message [Move on to the ?pos column 
					from the right.This is the ?col-name 
					column.])
	(construct-success-message [Good job! You are in the ?pos column 
					from the right. This is the ?col-name column.])
    (bind ?current-sub-goal (assert (process-column-goal
		(column ?next-column)
		(carry ?num0)
		(first-addend ?num1)
		(second-addend ?num2))))
	(modify ?work-to-do
		(begin-rule focus-on-next-column))
	(modify ?problem
			(subgoals (create$ ?current-sub-goal ?work-to-do)))
)

;; ADD-ADDENDS
;; IF
;;    There is a goal to process column C
;; THEN
;;    Set Sum to the sum of the addends in column C
;;    And set a subgoal to write Sum as the result in column C
;;    And remove the goal to process column C

(defrule add-addends
;	(addition 
;		(problem ?problem))
	?problem <- (problem 
		(subgoals $?sg1 ?subgoal $?sg2))
	?subgoal <- (process-column-goal
		(carry ?carry)
		(first-addend ?num1&:(neq ?num1 nil))
		(second-addend ?num2&:(neq ?num2 nil))
		(column ?column)
		(sum nil))
=>
	(bind ?sum (+ ?num1 ?num2))
	(construct-message [You need to add the 	
			two digits in this column. Adding ?num1 and ?num2 
			gives ?sum .])
    (modify	?subgoal
		(sum ?sum))	
;	(printout t "Add addends." crlf)
)

;; ADD-CARRY
;; IF
;;    There is a goal to write Sum as the result in column C
;;    And there is a carry into column C
;;    And the carry has not been added to Sum
;; THEN
;;    Change the goal to write Sum+1 as the result
;;    And mark the carry as added

(defrule add-carry
;	(addition 
;		(problem ?problem))
	?problem <- (problem
		(subgoals $? ?subgoal $?))
	?subgoal <- (process-column-goal
		(sum ?sum&:(neq ?sum nil))
		(carry ?num0&:(neq ?num0 nil))
		(first-addend ?num1)
		(second-addend ?num2))
	(test (neq ?num0 nil))
=>
	(bind ?new-sum (+ ?sum ?num0))
	(construct-message [There is a carry in to 
			this column so you need to add the value carried 
			in. This gives ?sum + 1 equals ?new-sum .])
    (modify ?subgoal
		(sum ?new-sum)
		(carry nil))
)

;; MUST-CARRY
;; IF
;;    There is a goal to write Sum as the result in column C
;;    And the carry into column C (if any) has been added to Sum
;;    And Sum > 9
;;    And Next is the column to the left of C
;; THEN
;;    Change the goal to write Sum-10 as the result in C
;;    Set a subgoal to write 1 as a carry in column Next

(defrule must-carry
;	(addition 
;		(problem ?problem))
	?problem <- (problem 
		(interface-elements $? ?table $?)
		(subgoals $?sg1 ?subgoal $?sg2))
	?subgoal <- (process-column-goal
		(sum ?sum&:(neq sum nil))
		(carry nil)
		(column ?column))
	(test (numberp ?sum))
	(test (> ?sum 9))
	?column <- (column
		(name ?column-name))
	?table <- (table
		(columns $? ?next-column ?column $?))
	?next-column <- (column
		(position ?next-pos))
=>
	(bind ?new-sum (- ?sum 10))
	(construct-message [The sum that you have ?sum 
			is greater than 9. So you need to carry 10 of the 
			?sum to the ?next-pos column. And you need to write 
			the rest of the sum at the bottom of the ?column-name 
			column.])
    (modify ?subgoal
		(sum ?new-sum))
	(bind ?write-carry-goal (assert (write-carry-goal
		(column ?next-column)
		(carry 1))))
	(modify ?problem
		(subgoals (create$ $?sg1 ?write-carry-goal ?subgoal $?sg2)))
)

;; WRITE-SUM
;; IF
;;    There is a goal to write Sum as the result in column C
;;    And Sum < 10
;;    And the carry into column C (if any) has been added
;; THEN
;;    Write Sum as the result in column C
;;    And remove the goal

(defrule write-sum
;	(addition
;		(problem ?problem))
	?problem <- (problem
		(subgoals $?sg1 ?subgoal $?sg2))
	?subgoal <- (process-column-goal
		(sum ?sum&:(neq ?sum nil))
		(column ?column)
		(carry nil))
	(test (< ?sum 10))
	?column <- (column 
		(position ?pos)
		(cells $? ?result))
	?result <- (cell
			(name ?cell-name))
=>
	(construct-message [Write sum ?sum at the 
				bottom of the ?pos column.])
    (predict-observable-action ?cell-name "UpdateTable" ?sum)
    (modify ?result
            (value ?sum))
	(modify ?problem 
		(subgoals (create$ $?sg1 $?sg2)))
	(retract ?subgoal)
)

;; WRITE-CARRY
;; IF
;;    There is a goal to write a carry in column C
;;    And there is no result that has been recorded in the previous column
;;    And sum has been calculated in previous column P
;; THEN
;;    Write the carry in column C
;;    And remove the goal

(defrule write-carry
;	(addition
;		(problem ?problem))
	?problem <- (problem
		(subgoals $?sg1 ?subgoal $?sg2)
		(interface-elements $? ?table $?))
	?subgoal <- (write-carry-goal
		(carry ?num)
		(column ?column))
	?column <- (column
		(position ?pos)
		(cells ?carry $?))
	?carry <- (cell
		(name ?cell-name)
		(value nil))
	?table <- (table
		(columns $? ?column ?previous-column $?))
	?previous-column <- (column
		(position ?pos-previous)
	        (cells $? ?sum))
        ?sum <- (cell
;		(value ?val&:(neq ?val nil))
		)
=>
	(printout t crlf "Selection: " ?cell-name "  Action: 'UpdateTable'  Input: " ?num crlf)
    (construct-message [You need to complete 
					the work on the ?pos-previous column.] 
				[Write carry from the ?pos-previous 
					to the next column.]
				[Write ?num at the top of the ?pos column 
					from the right.])
	(predict-observable-action ?cell-name "UpdateTable" ?num)
    (modify ?carry
		(value ?num))
	(modify ?problem
		(subgoals (create$ ?sg1 ?sg2))) 	; the remaining subgoals
	(retract ?subgoal)
;      (printout t "Write-carry." crlf)
)

;; BUGGY-FOCUS-ON-FIRST-COLUMN
;; IF
;;    The goal is to do an addition problem
;;    And there is no pending subgoal (i.e., we've just started the problem)
;;    And C is a column of the table but NOT the rightmost column 
;; THEN
;;    Set a subgoal to process column C
;;    Set an error message "Start with the column all the way to the right, the ones column.  You've started in another column.

(defrule BUGGY-focus-on-first-column 
;	(addition 
;		(problem ?problem))
	?problem <- (problem 
		(subgoals )
		(interface-elements $? ?table $?))
	?table <- (table 
		(columns $? ?wrong-column $? ?right-column))
	?wrong-column <- (column 
		(cells $? ?wrong-cell $?))
	?right-column <- (column 
		(cells $? ?first-addend ?second-addend ?result))
	?wrong-cell <- (cell 
	    (name ?wrong-cell-name)
		(value nil))
	?first-addend <- (cell 
		(value ?num1))
	?second-addend <- (cell 
		(value ?num2))
	?result <- (cell 
	    (name ?cell-name)
		(value nil))
=>
	(construct-message [Start with the column all the way to the
				right, the ones column.  You've started in another column.])
	(predict-observable-action ?wrong-cell-name DONT-CARE DONT-CARE)
    (bind ?current-sub-goal (assert (process-column-goal
		(column ?right-column)
		(first-addend ?num1)
		(second-addend ?num2))))
	(modify ?problem
		(subgoals ?current-sub-goal))
)

;; BUGGY-WRITE-CARRY
;; IF
;;    There is a goal to write a carry in column C
;;    And sum has not yet been calculated in previous column P
;; THEN
;;    Write the carry in column C
;;    And remove the goal

;(defrule Buggy-write-carry
;	(addition
;		(problem ?problem))
;	?problem <- (problem
;		(subgoals $?sg1 ?subgoal $?sg2)
;		(interface-elements $? ?table $?))
;	?subgoal <- (write-carry-goal
;		(carry ?num)
;		(column ?column))
;	?column <- (column
;		(position ?pos)
;		(cells ?carry $?))
;	?carry <- (cell
;		(name ?cell-name)
;		(value nil))
;	?table <- (table
;		(columns $? ?column ?previous-column $?))
;	?previous-column <- (column
;		(position ?pos-previous)
;	        (cells $? ?sum))
;       ?sum <- (cell
;		(value ?val&:(eq ?val nil)))
;	
;=>
;	(modify ?carry
;		(value ?num))
;	(modify ?problem
;		(subgoals ?sg1 ?sg2)) 	; the remaining subgoals
;	(predict-observable-action ?cell-name "UpdateTable" ?num)
;	(construct-message [You need to write the 
;				sum before doing the carry.])
;	(retract ?subgoal)
;)
  
;; DONE
;; IF
;;   There is a goal to process column C
;;   And all values in the process-column goal are nil
;; THEN
;;   Press the Done button
;;   And remove all subgoals

(defrule done 
	?problem <- (problem 
		(subgoals $?sg1 ?subgoal $?sg2)
		(interface-elements $? ?table $?))
	?subgoal <- (process-column-goal
		(column ?next-column)
		(carry nil)
		(first-addend nil)
		(second-addend nil))
	?table <- (table
		(columns $? ?next-column $?))
  =>
	(predict-observable-action done ButtonPressed DONT-CARE)
	(construct-message "[Any work left to do?]" "[You are finished. Click on the Done Button.]")
	(modify ?problem
		(subgoals ))
)

;(defrule Done 
;	?problem <- (problem 
;		(subgoals ?subgoal)
;		(interface-elements $? ?table $?))
;	?subgoal <- (finish-problem-goal
;		(begin-rule ?beginning-rule))
;	?table <- (table 
;		(columns $? ?a-column))
;	?a-column <- (column 
;		(cells $? ?first-addend ?second-addend ?result))
;	?first-addend <- (cell 
;		(value ?num1&:(neq ?num1 nil)))
;	?second-addend <- (cell 
;		(value ?num2&:(neq ?num2 nil)))
;	?result <- (cell 
;	        (name ?cell-name)
;		(value ?num3&:(neq ?num3 nil)))
;	
;=>
;	(printout t crlf "Problem done; began with rule " ?beginning-rule crlf)
;	(predict-observable-action done ButtonPressed -1)
;	(construct-message [ Click on the Done ;Button. ] )
;)

;; Rules to test WhyNot's line numbering. 
;; These should never fire.
;; IF
;;   2 is less than 1
;; THEN
;;   print a message
;;
(defrule WhyNot-line-test-1
  (declare (salience -100))
  (test (< 2 1))
=>
  (printout t "2 is less than 1" crlf)
)

;; Give notice that rules have been read
(provide rules)
