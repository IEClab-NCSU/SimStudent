# Parse tree node for unary minus & unary plus

goog.provide('CTATUnaryNode')
goog.require('CTATTreeNode')

class CTATUnaryNode extends CTATTreeNode
  constructor: (@operator, @base, @parens = 0, @sign = 1, @exp = 1) ->
  clone: -> new CTATUnaryNode @operator, @base, @parens, @sign, @exp
  toString: -> super "#{CTATTreeNode.toOperatorString(@operator)}#{@base.toString()}"
  evaluate: -> @sign = - @sign if @negation(); super @base.evaluate()
  equals: (node) -> super(node) and @base.equals(node.base)
  simplify: (@methods) -> @base = @base.simplify(@methods); super

  flatten: -> super; @negate() if @negation(); @pushNegation().pushInversion().base

  compare: (node, reverse) -> super or @compareSigns(node, reverse)
  countVariables: -> @base.countVariables()

  pushNegation: -> (@negate(); @base.negate()) if @negated(); @
  pushInversion: -> (@invert(); @base.invert()) if @inverted(); @

if module? then module.exports = CTATUnaryNode else @CTATUnaryNode = CTATUnaryNode
